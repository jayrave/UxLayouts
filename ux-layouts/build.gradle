apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    publishedGroupId = 'com.jayrave.layouts'
    publishedArtifactId = 'ux-layouts'

    bintrayRepo = 'android'
    bintrayName = publishedArtifactId

    libraryName = 'UxLayouts'
    libraryVersionName = '1.0'
    libraryDescription = 'Layouts to provide better UX to android users'

    siteUrl = 'https://github.com/jayrave/UxLayouts'
    gitUrl = 'https://github.com/jayrave/UxLayouts.git'
    issuesUrl = 'https://github.com/jayrave/UxLayouts/issues'

    developerId = 'jayrave'
    developerName = 'Jayanthan Raveendiran'
    developerEmail = 'jayanthan.raveendiran@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ['Apache-2.0']
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName libraryVersionName
    }
}


dependencies {
    compile 'com.jayrave.layouts:percent-layouts:1.1'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}


task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives javadocJar
    archives sourcesJar
}


group = publishedGroupId
version = libraryVersionName


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId publishedArtifactId

                name libraryName
                description libraryDescription
                url siteUrl

                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }

                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties['bintray.user']
    key = properties['bintray.apikey']

    publish = true
    configurations = ['archives']

    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        labels = ['aar', 'android', 'ux', 'layout']
        publicDownloadNumbers = true

        version {
            name = libraryVersionName
            released = new Date()
        }
    }
}
